prefix dwc: <http://rs.tdwg.org/dwc/terms/> 
prefix obo: <http://purl.obolibrary.org/obo/>  
SELECT   ?startDayOfYear ?year ?latitude ?longitude ?wholePlant
WHERE {    
	?wholePlant dwc:genus "Helianthus"^^<http://www.w3.org/2001/XMLSchema#string> . 
	optional{ ?wholePlant dwc:specificEpithet "annuus"^^<http://www.w3.org/2001/XMLSchema#string> } . 

	# wholePlant 'hasQuality' some plantStructurePresence
	?wholePlant obo:RO_0000086 ?plantStructurePresence . 

	# search for flower heads present
	#?plantStructurePresence rdf:type obo:PPO_0003010 . 

	# plantStructurePresence 'has quality measurement' some measurementDatum
	?plantStructurePresence obo:PPO_0001007 ?measurementDatum . 
	
	# measurementDatum 'is_specified_output_of' some phenologyObservingProcess
	?measurementDatum obo:OBI_0000312 ?phenologyObservingProcess . 

	# set the type for phenologyObservingProcess and return properties
	?phenologyObservingProcess rdf:type obo:PPO_0002000 . 
	optional {?phenologyObservingProcess dwc:startDayOfYear ?startDayOfYear} . 
	optional {?phenologyObservingProcess dwc:year ?year} . 
	optional {?phenologyObservingProcess dwc:decimalLatitude ?latitude} . 
	optional {?phenologyObservingProcess dwc:decimalLongitude ?longitude} . 
} 
