prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
SELECT  ?startDayOfYear ?year ?latitude ?longitude ?genus ?traitLabel ?plantStructurePresenceType
WHERE {    
	?wholePlant dwc:genus ?genus . 

	?wholePlant obo:RO_0000086 ?plantStructurePresence . 

	# Query all subClasses of 'plant structures present'
	# Note: still need to be able to query for * 'absent'
	?plantStructurePresence rdf:type ?plantStructurePresenceType . 
	?plantStructurePresenceType rdfs:subClassOf* <http://purl.obolibrary.org/obo/PPO_0003000> . 
	?plantStructurePresenceType rdfs:label ?traitLabel .

	?plantStructurePresence obo:PPO_0001007 ?measurementDatum . 
	
	?measurementDatum obo:OBI_0000312 ?phenologyObservingProcess . 

	# set the type for phenologyObservingProcess and return properties
	?phenologyObservingProcess rdf:type obo:PPO_0002000 . 
	?phenologyObservingProcess dwc:startDayOfYear ?startDayOfYear . 
	?phenologyObservingProcess dwc:year ?year . 
	?phenologyObservingProcess dwc:decimalLatitude ?latitude . 
	?phenologyObservingProcess dwc:decimalLongitude ?longitude . 
} ORDER BY ?startDayOfYear ?year
